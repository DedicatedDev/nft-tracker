
function _transfer_ownership(chain: name, contract: byte_array, nft_id: integer, owner_id: byte_array, block_number: integer) {
    require(
        op_context.is_signer(chain_context.args.admin_pubkey), 
        "Owership: Only an oracle can transfer ownership"
    );

    val owner = owner @? { owner_id } ?: create owner(owner_id);
    val nft = nft @? { chain, contract, nft_id } ?: create nft (chain, contract, nft_id);
    
    before_transfer_ownership(chain, contract, nft_id, owner_id);
    
    val ownership = ownership @? { owner, nft, block_number } ?: create ownership (
        owner = owner,
        nft = nft,
        block_number
    );

    val current_owner = current_owner @? { nft };
    if (current_owner != null and current_owner.ownership.block_number > block_number) {
        return;
    }

    // Chain current owner of nft
    delete current_owner @? { nft };
    create current_owner (
        ownership = ownership,
        nft = nft
    );
    
    after_transfer_ownership(chain, contract, nft_id, owner_id);
}

function _add_contract(chain: text, address: byte_array, type: text) {
    require(
        op_context.is_signer(chain_context.args.admin_pubkey), 
        "ownership: only admin can add contracts!"
    );

    val _contract = contract@? {
        .chain == chain,
        .address == address,
        .type == type
    } ?: create contract(chain, address, type);

    create sync_status(
        contract = _contract,
        last_block_number = 0
    );
    return _contract;
}

function _update_contract_sync_status(chain: text, address: byte_array, type: text, last_block_number:integer) {
    val _contract = contract @? { .chain == chain, .address == address } ?: _add_contract(chain, address, type);
    val status = sync_status @ { .contract == _contract};
    if(status.last_block_number < last_block_number) {
        status.last_block_number = last_block_number;
    }
}

function _get_contracts() {
    return (c:contract, s: sync_status) @* { 
        s.contract == c 
    } ( .chain, 
        .address, 
        .type, 
        .last_block_number
    );
}

function _get_nft_history(chain: text, contract: byte_array, id: integer, cursor: page_cursor) {
    val nft = nft @? { chain, contract, id };
    
    if (empty(nft)) return null_page();
    
    val nft_history = ownership @* { nft, .rowid >= cursor.after_rowid } (
        data = (
            owner = .owner.id
        ).to_gtv(),
        rowid = .rowid
    ) limit fetch_data_size(cursor);
    
    return _make_page(nft_history, cursor); 
}

function _get_user_nfts(owner_id: byte_array, cursor: page_cursor) {
    val owner = owner @? { owner_id };
    if (empty(owner)) return null_page();
    val user_nfts = current_owner @* { 
        .ownership.owner == owner, 
        .rowid >= cursor.after_rowid
    } (
        data = (
            chain = .nft.chain,
            contract = .nft.contract,
            id = .nft.id
        ).to_gtv(),
        rowid = .rowid
    ) limit fetch_data_size(cursor);
    return _make_page(user_nfts,cursor);
}

function _get_nfts_by_token(chain: text, contract: byte_array, cursor: page_cursor) {
    val nft_collection = ownership @* {
        .nft.chain == chain,
        .nft.contract == contract,
        .rowid >= cursor.after_rowid 
    } (
        data = (
            chain = .nft.chain,
            contract = .nft.contract,
            id = .nft.id
        ).to_gtv(),
        rowid = .rowid
    ) limit fetch_data_size(cursor);
    
    return _make_page(nft_collection,cursor);
}

function _make_page(query_data: list<(data: gtv, rowid: rowid)>, cursor: page_cursor) {
    if (query_data.size() == 0) return null_page();
    
    if (query_data.size() > page_size(cursor)) {
        val query_data_to_return = query_data.sub(0, query_data.size()-1);
        val last_item = query_data_to_return[query_data_to_return.size()-1];
        val last_item_rowid = last_item.rowid;
        val next_cursor = page_cursor(
            page_size = cursor.page_size,
            after_rowid = last_item_rowid
        );

        return (
            cursor = ( 
                next = encode_cursor(next_cursor)
            ),
            data = query_data_to_return @* {} .data
        );
    } else {
        return (
            cursor = ( 
                next = null
            ),
            data = query_data  @* {} .data
        );
    }
}


function null_page() = (
    cursor = (
        next = null
    ),
    data = list<gtv>()
);

function encode_cursor(page_cursor) = page_cursor.to_bytes().to_base64();

function decode_cursor(cursor: text) = page_cursor.from_bytes(byte_array.from_base64(cursor));

function page_size(page_cursor) = min(MAX_PAGE_SIZE, page_cursor.page_size);

function fetch_data_size(page_cursor) = page_size(page_cursor) + 1;

@extendable function before_transfer_ownership(chain: name, contract: byte_array, nft_id: integer, owner_id: byte_array);

@extendable function after_transfer_ownership(chain: name, contract: byte_array, nft_id: integer, owner_id: byte_array);

